;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; View
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.view jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.image)
        (jazz.jml)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jiri.font)
        (jiri.settings)
        (jiri.window))


(definition public (add-view view)
  (unless (get-parent~ view)
    (set-parent~ view (first-child~ (current-stage)))
    (invalidate-view~ view)))


(definition public (remove-view view)
  (set-parent~ view #f)
  (invalidate-view~ view))
  

;;;
;;;; Background
;;;


(definition cached-background
  #f)


(definition public (cache-background)
  (when (not cached-background)
    (let ((background (jiri-background)))
      (assert background
        (set! cached-background (load-image background)))))
  cached-background)


(definition public (cache-background-size)
  (let ((background (cache-background)))
    (new Dimension
      (cairo_image_surface_get_width background)
      (cairo_image_surface_get_height background))))


;;;
;;;; Root
;;;


(class Jiri-Root extends View
  
  
  (method override (draw surface context)
    (draw-surface~ surface (cache-background) 0 0)
    (let ((rect (get-bounds)))
      (let ((right (get-right~ rect))
            (bottom (get-bottom~ rect)))
        (fill-rect~ surface (new Rect 20 (- bottom 150) (- right 20) (- bottom 20)) {Color Black alpha: .5}))))
  
  
  (method override (cursor-update evt)
    (set-cursor :arrow)))


;;;
;;;; Title
;;;


(class Jiri-Title extends View
  
  
  (cond-expand
    (windows
     (slot cursor-pos initialize #f getter generate)
     (slot window-pos initialize #f getter generate)
     (slot moving?    initialize #f getter generate))
    (else))
  
  
  (method override (draw surface context)
    (set-font~ surface default-title-font)
    (draw-text~ surface 0 0 title {Color White}))
  
  
  (cond-expand
    (windows
     (method override (cursor-update evt)
       (set-cursor :all))

     (method override (mouse-down evt)
       (set! cursor-pos (to-desktop (get-position~ evt)))
       (set! window-pos (get-position~ (get-toplevel)))
       (set! moving? #t)
       (acquire-capture))
     
     (method override (drag-move evt)
       (when moving?
         (let ((current (to-desktop (get-position~ evt))))
           (let ((delta (nu- current cursor-pos)))
             (let ((pos (nu+ window-pos delta)))
               (set-position~ (get-toplevel) pos))))))

     (method override (mouse-up evt)
       (when moving?
         (release-capture)
         (set! moving? #f))))
    (else)))


;;;
;;;; Label
;;;


(class Jiri-Label extends Label-View
  
  
  (form
    (<install> color: {Color White})))


;;;
;;;; Button
;;;


(class Jiri-Button extends View
  
  
  (property action initialize #f accessors generate)
  
  
  (method override (draw surface context)
    (let ((bounds (get-bounds)))
      (if (and enabled? (eq? self (mouse-view)))
          (fill-rect~ surface bounds {Color red: 230 green: 0 blue: 0})
        (gradient-fill-rect~ surface bounds
          (list (list 0 (if enabled? {Color red: 150 green: 0 blue: 0} {Color White}))
                (list 1 (if enabled? {Color red: 220 green: 0 blue: 0} {Color White})))
          direction: 'horizontal)))
    (set-font~ surface default-button-font)
    (when title
      (let ((pos (justify-lazy (lambda () (get-text-extent~ surface title))
                               (lambda () (get-size))
                               'center)))
        (draw-text~ surface (get-h~ pos) (get-v~ pos) title (if enabled? {Color White} {Color red: 160 green: 160 blue: 160})))))
  
  
  (method override (cursor-update evt)
    (set-cursor :arrow))
  
  
  (method override (mouse-enter)
    (invalidate-view))
  
  
  (method override (mouse-leave)
    (invalidate-view))
  
  
  (method override (mouse-up evt)
    (when enabled?
      (process-action self))))


;;;
;;;; Close
;;;


(class Jiri-Close extends Jiri-Button
  
  
  (method override (draw surface context)
    (let ((rect (get-bounds)))
      (let ((left (+ (get-left~ rect) 2))
            (top (+ (get-top~ rect) 2))
            (right (- (get-right~ rect) 2))
            (bottom (- (get-bottom~ rect) 2)))
        (define (draw-x width color)
          (set-line-width~ surface width)
          (set-color~ surface color)
          (line~ surface left top right bottom)
          (line~ surface right top left bottom))
        
        @not-needed (draw-x 4. {Color red: 150 green: 150 blue: 150})
        (draw-x 2. (if (eq? self (mouse-view)) {Color red: 200 green: 0 blue: 0} {Color red: 255 green: 255 blue: 255})))))
  
  
  (method override (process-action sender (properties (action-properties)))
    (quit-callback)))


;;;
;;;; Minimize
;;;


(class Jiri-Minimize extends Jiri-Button
  
  
  (method override (draw surface context)
    (let ((rect (get-bounds)))
      (let ((left (+ (get-left~ rect) 2))
            (top (+ (get-top~ rect) 2))
            (right (- (get-right~ rect) 2))
            (bottom (- (get-bottom~ rect) 2)))
        (define (draw-line width color)
          (set-line-width~ surface width)
          (set-color~ surface color)
          (line~ surface left (- bottom 1) right (- bottom 1)))
        
        @not-needed (draw-line 4. {Color red: 150 green: 150 blue: 150})
        (draw-line 2. (if (eq? self (mouse-view)) {Color red: 200 green: 0 blue: 0} {Color red: 255 green: 255 blue: 255})))))
  
  
  (method override (process-action sender (properties (action-properties)))
    (minimize~ (get-toplevel))))


;;;
;;;; Progress
;;;


(class Jiri-Progress extends View


  (property limits initialize #f accessors generate)
  (property range  initialize #f accessors generate)
  (property pos    initialize #f getter generate setter explicit)
  
  
  (method override (draw surface context)
    (let ((rect (get-bounds)))
      (let ((left (get-left~ rect))
            (top (get-top~ rect))
            (right (get-right~ rect))
            (bottom (get-bottom~ rect)))
        (fill-rect~ surface rect {Color White})
        (let ((start (get-start~ range))
              (end (get-end~ range))
              (width (rect-width rect)))
          (let ((head (if (not limits) 0 (fxfloor (* (get-start~ limits) width))))
                (tail (if (not limits) width (fxceiling (* (get-end~ limits) width))))
                (where (/ (fixnum->flonum (- pos start)) (fixnum->flonum (- end start)))))
            (let ((h (fxceiling (* (- tail head) where)))
                  (from {Color red: 150 green: 0 blue: 0})
                  (to {Color red: 220 green: 0 blue: 0}))
              (gradient-fill-rect~ surface (new Rect left top (+ left head h) bottom)
                (list (list 0 from)
                      (list 1 to))
                direction: 'horizontal)))))))
  
  
  (method (set-info limits range)
    (set! limits~self limits)
    (set! range~self range)
    (set! pos~self (get-start~ range))
    (invalidate-view))
  
  
  (method (set-pos pos)
    (set! pos~self pos)
    (invalidate-view))
  
  
  (method (increase-pos)
    (set-pos (+ pos 1)))
  
  
  (method (get-remaining)
    (- (get-end~ range) pos))
  
  
  (method (set-done)
    (set-pos (get-end~ range)))))
