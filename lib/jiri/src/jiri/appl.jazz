;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Jiri Application
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.appl jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.snapshot)
        (jazz.platform)
        (jazz.system)
        (jazz.system.application)
        (jazz.ui)
        (jazz.ui.skin)
        (jazz.ui.view)
        (jazz.ui.window)
        (jiri.pane)
        (jiri.settings)
        (jiri.view)
        (jiri.window))


;;;
;;;; Application
;;;


(class Jiri-Application extends Application
  
  
  (method override (process-name)
    "Jiri")


  (method override (new-toplevel)
    (new Jiri-Toplevel))
  
  
  (method override (prepare-process)
    (nextmethod)
    (when (jiri-process-name)
      (current-process-name-set! (jiri-process-name)))
    (when (jiri-snapshots-directory)
      (snapshots-directory (jiri-snapshots-directory))))
  
  
  (method override (finish-process)
    (define (setup-toplevel)
      (let ((toplevel (get-toplevel)))
        (when (and (/= window-h -1)
                   (/= window-v -1))
          (set-position~ toplevel (new Point window-h window-v)))
        (set-size~ toplevel (cache-background-size))))
    
    (nextmethod)
    (set-skin (new Jiri-Skin))
    (maximize~ (current-stage))
    (new-frame Jiri-Pane)
    (init)
    (setup-toplevel)
    (delay-event startup)))


;;;
;;;; Toplevel
;;;


(class Jiri-Toplevel extends Toplevel


  (form
    (<install> caption-base: "Jiri" position: {Point 100 100} size: {Dimension 800 625}))
  
  
  (method override (user-notification what result)
    (when notification-callback
      (notification-callback what result))))


(cond-expand
  (cocoa
    (specific (window-resizable? (<Jiri-Toplevel> toplevel) rest)
      #f))
  (windows
    (specific (window-style (<Jiri-Toplevel> toplevel) rest)
      (bitwise-ior WS_CLIPSIBLINGS
                   WS_CLIPCHILDREN
                   WS_POPUP)))
  (else)))
