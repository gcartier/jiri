;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Install
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.install jazz


(import (jazz.git.foreign)
        (jazz.graphic)
        (jazz.io)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jiri.base)
        (jiri.git)
        (jiri.git-interface)
        (jiri.view)
        (jiri.settings)
        (jiri.structure)
        (jiri.window)
        (jiri.work))


(define TEST?
  #f)


(define stage
  #f)


(define (prepare)
  (when (and TEST? (not (getenv-default "called-from")))
    (setenv "root-dir" "C:/App/Sentio/")
    (setenv "called-from" "root"))
  
  (current-root-dir (getenv-default "root-dir"))
  (current-password (getenv-default "password"))
  (called-from (getenv-default "called-from"))
  (set! stage (cond ((and (current-root-dir)
                          (equal? (called-from) "setup"))
                     'install-from-setup)
                    ((and (current-root-dir)
                          (equal? (called-from) "root"))
                     'install-from-root)
                    ((and (current-root-dir)
                          (equal? (called-from) "current"))
                     'install-from-current)
                    (else
                     (message-box "It is incorrect to launch this application")
                     (exit 1))))
  (when (neq? stage 'install-from-root)
    (set-work-downloaded (string->number (getenv-default "work-downloaded" "0")))
    (set-window-h (string->number (getenv-default "window-h" "-1")))
    (set-window-v (string->number (getenv-default "window-v" "-1")))
    (set-send-ready? (getenv-default "send-ready" #f))))


;;;
;;;; Install
;;;


(define (install)
  (case stage
    ((install-from-setup) (install-from-setup))
    ((install-from-root) (install-from-root))
    ((install-from-current) (install-from-current))))


;;;
;;;; Install From Setup
;;;


(define (install-from-setup)
  (install-current)
  (install-root)
  (install-desktop)
  (install-start-menu)
  (update-start-menu)
  (install-uninstall)
  (install-application/world
    (lambda (new-content?)
      (install-done))))


;;;
;;;; Install From Root
;;;


(define (install-from-root)
  (pull-repository "launch" (jiri-install-remote) (jiri-install-branch) (current-password) (install-dir) 1 6 0. .05 .1
    (lambda (new-content?)
      (if new-content?
          (delegate-install (current-root-dir) (current-password) "current")
        (begin
          (update-start-menu)
          (rewind-start-menu)
          (install-application/world
            (lambda (new-content?)
              (install-done))))))))


;;;
;;;; Install From Current
;;;


(define (install-from-current)
  (install-current)
  (install-root)
  (update-start-menu)
  (rewind-start-menu)
  (install-application/world
    (lambda (new-content?)
      (install-done))))


;;;
;;;; Work
;;;


(define (install-application/world cont)
  (pull-repository "application" (jiri-app-remote) (jiri-app-branch) (current-password) (app-dir) 3 6 .1 .2 .4
    (lambda (new-content?)
      (cond ((world-dir)
             (pull-repository "world" (jiri-world-remote) (jiri-world-branch) (current-password) (world-dir) 5 6 .4 .85 1.
               cont))
            ((jiri-world-work)
             ((jiri-world-work) 5 6 .4 .85 1. cont))))))


;;;
;;;; Done
;;;


(define (install-done)
  (set-title~ stage-view (or (jiri-play-ready) "Ready to play!"))
  (set-color~ stage-view stage-ready-color)
  (set-title~ status-view "Done")
  (set-enabled?~ play-view #t)
  (set-cursor :arrow)
  (set-work-in-progress? #f)
  (set-work-done? #t))


(define (install-current)
  (let ((install-dir (make-directory (install-dir)))
        (current-dir (make-directory (current-dir))))
    (install-directory~ install-dir current-dir
      filter: (lambda (action pathname)
                (if (and (eq? action 'copy)
                         (pathname=? (get-parent~ pathname) install-dir)
                         (filename=? (get-name~ pathname) ".git"))
                    #f
                  #t)))))


(define (install-root)
  (let ((from-exe (make-file (launch-exe)))
        (from-repo (make-file (launch-repo)))
        (from-lib (make-directory (launch-lib)))
        (to-exe (make-file (root-exe)))
        (to-repo (make-file (root-repo)))
        (to-lib (make-directory (root-lib))))
    (install-file~ from-exe to-exe)
    (install-file~ from-repo to-repo)
    (install-directory~ from-lib to-lib
      filter: (lambda (action pathname)
                (if (and (eq? action 'copy)
                         (pathname=? (get-parent~ pathname) from-lib)
                         (let ((filename (get-name~ pathname)))
                           (member? filename '("jazz.catalog"
                                               "jazz.debugger"
                                               "jazz.debugger.jazz"
                                               "jazz.designer"
                                               "jazz.editor.jazz"
                                               "jazz.editor.lisp"
                                               "jazz.git"
                                               "jazz.graphic"
                                               "jazz.jml"
                                               "jazz.jrm"
                                               "jazz.library"
                                               "jazz.media"
                                               "jazz.profile"
                                               "jazz.resources"
                                               "jazz.system"
                                               "jazz.ui")
                             test: filename=?)))
                    #f
                  #t)))))


(define (install-desktop)
  @convert
  (let ((path (root-exe))
        (shortcut (desktop-shortcut)))
    (let ((hr (create-shortcut path #f shortcut (jiri-title))))
      (when (< hr 0)
        (error "Unable to create desktop shortcut (0x" (number->string hr 16) ")")))))


(define (install-start-menu)
  @convert
  (let ((root (root-exe))
        (appdir (start-menu-appdir)))
    (when (file-exists? appdir)
      ;; danger
      (remove-directory appdir))
    (create-directory appdir)
    (let ((shortcut (start-menu-shortcut appdir)))
      (let ((hr (create-shortcut root #f shortcut (jiri-title))))
        (when (< hr 0)
          (error "Unable to create start menu shortcut (0x" (number->string hr 16) ")"))))))


(define (update-start-menu)
  @convert
  (let ((root (root-exe))
        (appdir (start-menu-appdir)))
    (when (file-exists? appdir)
      (let ((title "Video Card Information"))
        (let ((shortcut (string-append appdir "/" title ".lnk")))
          (let ((hr (create-shortcut root "-information" shortcut title)))
            (when (< hr 0)
              (error "Unable to create start menu shortcut (0x" (number->string hr 16) ")"))))))))


;; hack around windows taking forever to remove newly installed highlight
(define (rewind-start-menu)
  @convert
  (let ((shortcut (start-menu-shortcut (start-menu-appdir))))
    (when (file-exists? shortcut)
      (rewind-creation-time shortcut))))


(define (install-uninstall)
  @convert
  (let ((key (registry-create-key (HKEY_CURRENT_USER) (uninstall-subkey))))
    (registry-set-string key "DisplayName" (jiri-title))
    (registry-set-string key "DisplayIcon" (pathname-platformize (root-exe)))
    (registry-set-string key "DisplayVersion" (jiri-version))
    (registry-set-string key "Publisher" (jiri-company))
    (registry-set-string key "InstallDate" (get-local-date))
    (registry-set-string key "InstallLocation" (pathname-platformize (current-root-dir)))
    (registry-set-string key "UninstallString" (string-append (pathname-platformize (root-exe)) " -uninstall"))
    (registry-set-int key "EstimatedSize" (jiri-size))
    (registry-set-int key "NoModify" 1)
    (registry-set-int key "NoRepair" 1)
    (registry-close-key key)))


;;;
;;;; Layout
;;;


(define (layout)
  (add-view root-view)
  (add-view invite-view)
  (when close-view
    (add-view close-view))
  (when minimize-view
    (add-view minimize-view))
  (let ((remotes (jiri-app-remotes))
        (default (jiri-app-default)))
    (when remotes
      (add-view remotes-view)
      (let ((tree (locate~ remotes-view 'tree)))
        (for-each (lambda (env)
                    (add-row~ tree children: (list (new Tree-Node title: (car env))) user-data: env))
                  remotes)
        (when default
          (select-user-data-row~ tree (assoc default remotes))))))
  (add-view percentage-view)
  (add-view downloaded-view)
  (add-view status-view)
  (add-view remaining-view)
  (add-view progress-view)
  (add-view play-view)
  (if (eq? stage 'install-from-setup)
      (begin
        (add-stage-view "Setup successful!" stage-install-color)
        @convert
        (thread-start!
          (make-thread
            (lambda ()
              (thread-sleep! 2.5)
              (PostMessage (window-handle current-window) WM_USER UPDATING_GAME 0)))))
    (add-stage-view (string-append "Updating " (game-string)) stage-install-color))
  (when (neq? stage 'install-from-root)
    (set-title~ downloaded-view (string-append "Downloaded: " (number->string work-downloaded) "M"))
    (set-title~ status-view (downloading-title "application" 3 6))
    (set-info~ progress-view (new Range .1 .4) (new Range 0 10)))
  (update-percentage)
  (set-return-callback
    (lambda ()
      (when work-done?
        (play))))
  (set-quit-callback
    (quit-safely)))


;;;
;;;; Init
;;;


(set-init
  (lambda ()
    (prepare)
    (layout)))


;;;
;;;; Startup
;;;


(set-startup
  (lambda ()
    (install))))
