;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Jiri Task
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.task jazz


(definition task-invalidater
  #f)

(definition protected (set-task-invalidater proc)
  (set! task-invalidater proc))


(class Jiri-Task extends Object
  
  
  (slot status     initialize #f  getter generate)
  (slot part       initialize #f  getter generate)
  (slot doneable?  initialize #t  getter generate setter explicit)
  (slot range      initialize #f  getter generate setter explicit)
  (slot pos        initialize #f  getter generate setter explicit)
  (slot downloaded initialize #f  getter generate setter explicit)
  (slot remaining  initialize #f  getter generate setter explicit)
  (slot children   initialize '() accessors generate)
  
  
  (method override (initialize self (part: part #f) (doneable?: doneable? #t))
    (set! self.part part)
    (set! self.doneable? doneable?)
    (set! self.range (new Range 0 1))
    (set! self.pos 0))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" status))))
  
  
  (method public (set-status self status)
    (set! self.status status)
    (task-invalidater self))
  
  
  (method public (set-range self range)
    (set! self.range range)
    (set! self.pos (get-start range)))
  
  
  (method public (set-pos self pos)
    (set! self.pos pos)
    (task-invalidater self))
  
  
  (method public (done? self)
    (if doneable?
        (= pos (get-end range))
      #f))
  
  
  (method public (set-done self)
    (set-pos self (get-end range)))
  
  
  (method public (progress-done self)
    (/ (fixnum->flonum (- pos (get-start range))) (fixnum->flonum (extent range))))
  
  
  (method public (set-downloaded self value)
    (set! downloaded value)
    (task-invalidater self))
  
  
  (method public (set-remaining self value)
    (set! remaining value)
    (task-invalidater self))
  
  
  (method public (add-child self child)
    (set! children (append children (list child)))
    child)))
