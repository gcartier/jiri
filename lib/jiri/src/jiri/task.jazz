;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Jiri Task
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.task jazz


(definition task-invalidater
  #f)

(definition protected (set-task-invalidater proc)
  (set! task-invalidater proc))


(class Jiri-Task extends Object
  
  
  (slot status     initialize #f  accessors generate)
  (slot part       initialize #f  getter generate)
  (slot range      initialize #f  getter generate setter explicit)
  (slot pos        initialize #f  getter generate setter explicit)
  (slot downloaded initialize #f  getter generate setter explicit)
  (slot remaining  initialize #f  getter generate setter explicit)
  (slot children   initialize '() accessors generate)
  
  
  (method override (initialize self (part: part #f))
    (set! self.part part)
    (set! self.range (new Range 0 1))
    (set! self.pos 0))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" status))))
  
  
  (method public (set-range self range)
    (set! self.range range)
    (set! self.pos (get-start range)))
  
  
  (method public (set-pos self pos)
    (set! self.pos pos)
    (task-invalidater self))
  
  
  (method public (increase-pos self)
    (set! pos (+ pos 1))
    (task-invalidater self))
  
  
  (method public (done? self)
    (= pos (get-end range)))
  
  
  (method public (set-done self)
    (set-pos self (get-end range)))
  
  
  (method public (progress-done self)
    (/ (fixnum->flonum (- pos (get-start range))) (fixnum->flonum (extent range))))
  
  
  (method public (set-downloaded self value)
    (set! downloaded value)
    (task-invalidater self))
  
  
  (method public (set-remaining self value)
    (set! remaining value)
    (task-invalidater self))
  
  
  (method public (decrease-remaining-if self)
    (when (> remaining 0)
      (decrease! remaining))
    (task-invalidater self))
  
  
  (method public (add-child self child)
    (set! children (append children (list child)))
    child)))
