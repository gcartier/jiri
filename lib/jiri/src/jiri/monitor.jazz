;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Jiri Monitoring
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.monitor jazz


(import (jazz.event)
        (jazz.io)
        (jazz.ui)
        (jazz.view)
        (jiri.base)
        (jiri.settings)
        (jiri.structure)
        (jiri.window)
        (jiri.work))


;;;
;;;; Crashes
;;;


(definition protected send-crash-reports-thread
  #f)

(definition protected send-crash-reports-quit?
  #f)


(definition public (send-crash-reports monitor application?)
  (when monitor
    (let ((curl (curl-path)))
      (when (file-exists? curl)
        (continuation-capture
          (lambda (stop)
            (define (send-crash crash no total)
              (bind (file remote) crash
                (define (normalize-file file)
                  (let ((name (get-name file)))
                    (if (find name #\space)
                        (let ((brother-name (substitute #\space #\_ name)))
                          (let ((brother (new-brother file brother-name)))
                            (rename file brother)
                            brother))
                      file)))
                
                (let ((file (normalize-file file)))
                  (let ((port (open-process
                                (list
                                  path: curl
                                  arguments: (list "--silent" "-T" (parse file) "--limit-rate" "250K" "-H" "Content-Type: text/plain" (string-append remote "/" (get-name file)))
                                  show-console: #f))))
                    (set-title crashes-view (format "Sending logs ({a} of {a})" no total))
                    (let ((status (process-status port)))
                      (cond ((= status 0)
                             ;; robust to file still in use
                             (catch (os-exception? exc)
                               (delete file)))
                            (else
                             (set-title crashes-view (format "Sending logs error ({s})" status))
                             (continuation-return stop #f))))))))
            
            (define (send-crashes crashes total)
              (loop (for crash in crashes)
                    (for no from 1)
                    (send-crash crash no total)))
            
            (define (collect-crashes)
              (let ((queue (new-queue)))
                (for-each (lambda (info)
                            (when info
                              (bind (name dir extensions remote) info
                                (let ((effective-dir (if (string? dir)
                                                         (dirname->directory (string-append (current-root-dir) dir))
                                                       dir)))
                                  (when (exists? effective-dir)
                                    (iterate-directory effective-dir
                                      (lambda (file)
                                        (when (member? (get-extension file) extensions test: extension=?)
                                          (enqueue queue (list file remote))))
                                      files?: #t
                                      directories?: #f
                                      recursive?: #f))))))
                          monitor)
                (queue-list queue)))
            
            (let ((crashes (collect-crashes)))
              (let ((total (length crashes)))
                (when (> total 0)
                  (if (not application?)
                      (begin
                        (send-crashes crashes total)
                        (set-title crashes-view "Sending logs done")
                        (sleep 1)
                        (set-title crashes-view ""))
                    (let ((callback quit-callback))
                      (set-quit-callback
                        (lambda ()
                          (set! send-crash-reports-quit? #t)))
                      (thread-start!
                        (new-thread
                          (lambda ()
                            (set! send-crash-reports-thread (current-thread))
                            (send-crashes crashes total)
                            (set-title crashes-view "Sending logs done")
                            (set-quit-callback callback)
                            (set! send-crash-reports-thread #f)
                            (when send-crash-reports-quit?
                              (sleep 1)
                              (quit-callback)))
                          'send-crash-reports)))))))))))))


;;;
;;;; Curl
;;;


(cond-expand
  (windows
   (definition public (curl-path)
     (parse {File Build "curl.exe"})))
  (else
   (definition public (curl-path)
     "/usr/bin/curl"))))
