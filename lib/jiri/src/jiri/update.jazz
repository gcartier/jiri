;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Update
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.update jazz


(import (jazz.git.foreign)
        (jazz.io)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jiri.base)
        (jiri.devel)
        (jiri.git)
        (jiri.git-interface)
        (jiri.view)
        (jiri.settings)
        (jiri.structure)
        (jiri.window)
        (jiri.work))


;; MAC
;; Contents
;; - Info.plist
;; - App
;;   - update.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu (update)
;;         - lib
;;   - yownu.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu
;;         - lib
;; - MacOS
;;   - Yownu (launch)
;;   - lib
;; - Resources
;; - Worlds
;;   - yownu
;;     - .git


(define stage
  #f)


(define (prepare)
  (current-root-dir (getenv-default "root-dir"))
  (closed-beta-password (getenv-default "closed-beta-password"))
  (called-from (getenv-default "called-from"))
  (set! stage (cond ((and (not (current-root-dir))
                          (not (called-from)))
                     'launch)
                    ((and (current-root-dir)
                          (equal? (called-from) "launch"))
                     'update)
                    (else
                     (message-box "It is incorrect to launch this application")
                     (exit 1))))
  (when (eq? stage 'update)
    (set-work-percentage (string->number (getenv-default "work-percentage" "0.")))
    (set-work-downloaded (string->number (getenv-default "work-downloaded" "0")))
    (set-window-h (string->number (getenv-default "window-h" "-1")))
    (set-window-v (string->number (getenv-default "window-v" "-1")))))


(define (was-update-new?)
  (equal? (getenv-default "update-new" "yes") "yes"))


;;;
;;;; Update
;;;


(define (update)
  (case stage
    ((launch) (update-at-launch))
    ((update) (update-at-update))))


;;;
;;;; At Launch
;;;


(define (update-at-launch)
  (current-root-dir kernel-bundle-root)
  (let ((update-new? (not (file-exists? (update-dir)))))
    (add-stage-view "Updating launcher" (determine-stage-color (update-dir)))
    (pull-repository "launcher" (jiri-update-remote) (jiri-update-branch) (closed-beta-password) (update-dir) 1 6 0. .05 .1
      (lambda (new-content?)
        (if new-content?
            (delegate-update (current-root-dir) (closed-beta-password) "launch" update-new?)
          (update-application/world .1 .2 .4
            (lambda (new-content?)
              (update-done))))))))


(definition public (delegate-update root-dir closed-beta-password called-from update-new?)
  (setenv "root-dir" root-dir)
  (setenv "closed-beta-password" (or closed-beta-password ""))
  (setenv "called-from" called-from)
  (setenv "update-new" (if update-new? "yes" "no"))
  (setenv "work-percentage" (number->string work-percentage))
  (setenv "work-downloaded" (number->string work-downloaded))
  (let ((pos (get-position~ (get-toplevel))))
    (setenv "window-h" (number->string (get-h~ pos)))
    (setenv "window-v" (number->string (get-v~ pos))))
  (delegate-process (update-exe))
  ;; wait for delegate window to cover our own window
  (thread-sleep! .5)
  (exit))


;;;
;;;; At Update
;;;


(define (update-at-update)
  (add-stage-view "Updating launcher" (if (was-update-new?) stage-setup-color stage-install-color))
  (set-title~ status-view (updating-title "launcher" 1 6))
  (thread-start!
    (make-thread
      (lambda ()
        (update-launch .1 .2
          (lambda ()
            (update-application/world .2 .3 .4
              (lambda (new-content?)
                (update-done)))))))))


(define (update-launch head tail cont)
  (let ((update-dir (make-directory (update-dir)))
        (root-dir (make-directory (current-root-dir))))
    (define (install-file filename)
      (install-file~
        (new-file~ update-dir filename)
        (new-file~ root-dir filename)))
    
    (define (install-directory-macos dirname libdir)
      (install-directory~
        (new-directory~ update-dir dirname)
        (new-directory~ root-dir dirname)
        progress-feedback: (lambda (action dir)
                             (when (and (eq? action 'entering)
                                        (pathname=? (get-parent~ dir) libdir))
                               (post-event
                                 (lambda ()
                                   (increase-pos~ progress-view)
                                   (set-title~ remaining-view (string-append "Remaining: " (number->string (get-remaining~ progress-view))))))))))
    
    (define (install-directory dirname)
      (install-directory~
        (new-directory~ update-dir dirname)
        (new-directory~ root-dir dirname)))
    
    (let ((libdir (new-directory~ update-dir "Contents/MacOS/lib")))
      (let ((package-count (count-directories~ libdir)))
        (post-event
          (lambda ()
            (set-info~ progress-view (new Range head tail) (new Range 0 package-count))))
        (install-file "Contents/Info.plist")
        (install-directory-macos "Contents/MacOS" libdir)
        (install-directory "Contents/Resources")
        (post-event cont)))))


;;;
;;;; Work
;;;


(define (determine-stage-color dir)
  (if (not (file-exists? dir))
      stage-setup-color
    stage-install-color))


(define (update-application/world head mid tail cont)
  (add-stage-view "Updating game" (determine-stage-color (app-dir)))
  (pull-repository "game" (jiri-app-remote) (jiri-app-branch) (closed-beta-password) (app-dir) 3 6 .1 .2 .4
    (lambda (new-content?)
      (add-stage-view "Updating world" (determine-stage-color (world-dir)))
      (pull-repository "world" (jiri-world-remote) (jiri-world-branch) (closed-beta-password) (world-dir) 5 6 .4 .85 1.
        cont))))


;;;
;;;; Done
;;;


(define (update-done)
  (set-title~ stage-view "Ready to play!")
  (set-color~ stage-view stage-ready-color)
  (set-title~ status-view "Done")
  (set-title~ percentage-view "100%")
  (set-enabled?~ play-view #t)
  (set-cursor :arrow)
  (set-work-in-progress? #f)
  (set-work-done? #t))


;;;
;;;; Layout
;;;


(define (layout)
  (add-view root-view)
  (add-view invite-view)
  (when close-view
    (add-view close-view))
  (when minimize-view
    (add-view minimize-view))
  (add-view percentage-view)
  (add-view downloaded-view)
  (add-view status-view)
  (add-view remaining-view)
  (add-view progress-view)
  (add-view play-view)
  ;; mimick launcher state
  (when (eq? stage 'update)
    (set-title~ percentage-view (string-append (number->string (fxround work-percentage)) "%"))
    (set-title~ downloaded-view (string-append "Downloaded: " (number->string work-downloaded) "M"))
    (set-title~ status-view (installing-title "launcher" 1 6))
    (set-info~ progress-view (new Range 0. .1) (new Range 1 6))
    (set-done~ progress-view))
  (set-return-callback
    (lambda ()
      (when work-done?
        (play))))
  (set-quit-callback
    (quit-safely)))


;;;
;;;; Init
;;;


(set-init
  (lambda ()
    (prepare)
    (layout)))


;;;
;;;; Startup
;;;


(set-startup
  (lambda ()
    (update))))
