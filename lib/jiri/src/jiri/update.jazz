;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Update
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.update jazz


(import (jazz.git.foreign)
        (jazz.io)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jiri.base)
        (jiri.devel)
        (jiri.git)
        (jiri.git-interface)
        (jiri.view)
        (jiri.settings)
        (jiri.structure)
        (jiri.window)
        (jiri.work))


;; MAC
;; Contents
;; - Info.plist
;; - App
;;   - update.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu
;;         - lib
;;   - yownu.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu
;;         - lib
;; - MacOS
;;   - Yownu
;;   - lib
;; - Resources
;; - Worlds
;;   - yownu
;;     - .git


(define stage
  #f)


(define (prepare)
  (current-root-dir (getenv-default "root-dir"))
  (closed-beta-password (getenv-default "closed-beta-password"))
  (called-from (getenv-default "called-from"))
  (set! stage (cond ((and (not (current-root-dir))
                          (not (called-from)))
                     'root)
                    ((and (current-root-dir)
                          (equal? (called-from) "root"))
                     'update)
                    (else
                     (message-box "It is incorrect to launch this application")
                     (exit 1))))
  (when (eq? stage 'update)
    (set-work-percentage (string->number (getenv-default "work-percentage" "0.")))
    (set-work-downloaded (string->number (getenv-default "work-downloaded" "0")))
    (set-window-h (string->number (getenv-default "window-h" "-1")))
    (set-window-v (string->number (getenv-default "window-v" "-1")))))


;;;
;;;; Update
;;;


(define (update)
  (case stage
    ((root) (update-at-root))
    ((update) (update-at-update))))


;;;
;;;; At Root
;;;


(define (update-at-root)
  (current-root-dir kernel-bundle-root)
  (pull-repository "update" (jiri-update-remote) (jiri-update-branch) (closed-beta-password) (update-dir) 1 6 0. .05 .1
    (lambda (new-content?)
      (if new-content?
          (delegate-update (current-root-dir) (closed-beta-password) "root")
        (update-application/world
          (lambda (new-content?)
            (update-done)))))))


(definition public (delegate-update root-dir closed-beta-password called-from)
  (setenv "root-dir" root-dir)
  (setenv "closed-beta-password" (or closed-beta-password ""))
  (setenv "called-from" called-from)
  (setenv "work-percentage" (number->string work-percentage))
  (setenv "work-downloaded" (number->string work-downloaded))
  (let ((pos (get-position~ (get-toplevel))))
    (setenv "window-h" (number->string (get-h~ pos)))
    (setenv "window-v" (number->string (get-v~ pos))))
  (delegate-process (update-exe))
  ;; wait for delegate window to cover our own window
  (thread-sleep! .5)
  (exit))


;;;
;;;; At Update
;;;


(define (update-at-update)
  (update-root)
  (update-application/world
    (lambda (new-content?)
      (update-done))))


(define (update-root)
  (let ((update-dir (update-dir))
        (root-dir (current-root-dir)))
    (define (install-file filename)
      (let ((from (string-append update-dir filename))
            (to (string-append root-dir filename)))
        (install-file~ (new File (list from)) (new File (list to)))))
    
    (define (install-directory dirname)
      (let ((from (string-append update-dir dirname))
            (to (string-append root-dir dirname)))
        (install-directory~ (new Directory (list from)) (new Directory (list to)))))
    
    (install-file "Contents/Info.plist")
    (install-directory "Contents/MacOS")
    (install-directory "Contents/Resources")))


;;;
;;;; Work
;;;


(define (update-application/world cont)
  (pull-repository "application" (jiri-app-remote) (jiri-app-branch) (closed-beta-password) (app-dir) 3 6 .1 .2 .4
    (lambda (new-content?)
      (pull-repository "world" (jiri-world-remote) (jiri-world-branch) (closed-beta-password) (world-dir) 5 6 .4 .85 1.
        cont))))


;;;
;;;; Done
;;;


(define (update-done)
  (set-title~ stage-view "Ready to play!")
  (set-color~ stage-view stage-ready-color)
  (set-title~ status-view "Done")
  (set-title~ percentage-view "100%")
  (set-enabled?~ play-view #t)
  (set-cursor :arrow)
  (set-work-in-progress? #f)
  (set-work-done? #t))


;;;
;;;; Layout
;;;


(define (layout)
  (add-view root-view)
  (add-view invite-view)
  (when close-view
    (add-view close-view))
  (when minimize-view
    (add-view minimize-view))
  (add-stage-view "Updating game" (case stage
                                    ((root) stage-setup-color)
                                    ((update) stage-install-color)))
  (add-view percentage-view)
  (add-view downloaded-view)
  (add-view status-view)
  (add-view remaining-view)
  (add-view progress-view)
  (add-view play-view)
  (when (eq? stage 'update)
    (set-title~ percentage-view (string-append (number->string (fxround work-percentage)) "%"))
    (set-title~ downloaded-view (string-append "Downloaded: " (number->string work-downloaded) "M"))
    (set-title~ status-view (downloading-title "application" 3 6))
    (set-info~ progress-view (new Range .1 .4) (new Range 0 10)))
  (set-return-callback
    (lambda ()
      (when work-done?
        (play))))
  (set-quit-callback
    (quit-safely)))


;;;
;;;; Init
;;;


(set-init
  (lambda ()
    (prepare)
    (layout)))


;;;
;;;; Startup
;;;


(set-startup
  (lambda ()
    (update))))
