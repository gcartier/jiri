;;;==============
;;;  JiriScheme
;;;==============
;;;
;;;; Update
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module jiri.update jazz


(import (jazz.application)
        (jazz.event)
        (jazz.git.foreign)
        (jazz.io)
        (jazz.settings)
        (jazz.tree)
        (jazz.view)
        (jazz.window)
        (jiri.base)
        (jiri.git-interface)
        (jiri.manage)
        (jiri.settings)
        (jiri.structure)
        (jiri.task)
        (jiri.view)
        (jiri.window)
        (jiri.work))


;; MAC
;; Contents
;; - Info.plist
;; - App
;;   - update.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu (update)
;;         - lib
;;   - yownu.app
;;     - Contents
;;       - MacOS
;;         - .git
;;         - Yownu
;;         - lib
;; - MacOS
;;   - Yownu (launch)
;;   - lib
;; - Resources
;; - Worlds
;;   - yownu
;;     - .git


(define stage
  #f)


(define (prepare)
  (current-root-dir (string-argument "root-dir" #f))
  (current-password (string-argument "password" #f))
  (called-from (string-argument "called-from" #f))
  (set! stage (cond ((and (not (current-root-dir))
                          (not (called-from)))
                     'launch)
                    ((and (current-root-dir)
                          (equal? (called-from) "launch"))
                     'update)
                    (else
                     (message-box "It is incorrect to launch this application")
                     (exit 1))))
  (when (eq? stage 'update)
    (set-work-downloaded (integer-argument "work-downloaded" 0))
    (set-window-h (integer-argument "window-h" -1))
    (set-window-v (integer-argument "window-v" -1))
    (set-send-ready? (boolean-argument "send-ready" #f))))


(define (was-update-new?)
  (equal? (string-argument "update-new" "yes") "yes"))


;;;
;;;; Update
;;;


(define (update)
  (case stage
    ((launch) (update-at-launch))
    ((update) (update-at-update))))


(define (update-tasks-done)
  (for-each set-done~ (get-children~ (get-connection~ (jiri-update-section)))))


(define (transfer-tasks-setup proc)
  (let ((section (jiri-transfer-section)))
    (bind (transfer-task) (get-tasks~ section)
      (proc transfer-task))))


(define (work-offline exc)
  (define (debug-info)
    (if (jiri-devel?)
        (string-append " (" (get-message~ exc) ")")
      ""))
  
  (if (or (not (file-exists? (app-dir)))
          (not (file-exists? (data-dir))))
      (add-stage-view (string-append "Failed to install " (game-string) (debug-info)) stage-setup-color)
    (add-stage-view (string-append "Failed to update " (game-string) (debug-info)) stage-setup-color)))


;;;
;;;; At Launch
;;;


(define (update-at-launch)
  (current-root-dir kernel-bundle-root)
  (let ((update-new? (not (file-exists? (update-dir)))))
    (add-stage-view (string-append "Updating " (jiri-title)) (determine-stage-color (update-dir)))
    (catch (Git-Exception exc
             (if (jiri-offline?)
                 (work-offline exc)
               (exit 1)))
      (pull-repository (jiri-update-section) (current-password) (update-dir)
        (lambda (new-content?)
          (if new-content?
              (delegate-update (current-root-dir) (current-password) "launch" update-new?)
            (transfer-tasks-setup
              (lambda (transfer-task)
                (set-done~ transfer-task)
                (update-app/data
                  (lambda (new-content?)
                    (update-done)))))))
        #t))))


(definition public (delegate-update root-dir password called-from update-new?)
  (let ((arguments
          (build-arguments
            (lambda (add)
              (add "root-dir" root-dir)
              (when password
                (add "password" password))
              (add "called-from" called-from)
              (add "update-new" (if update-new? "yes" "no"))
              (add "work-downloaded" (number->string (or (calculate-downloaded) 0)))
              (let ((pos (get-position~ (get-toplevel))))
                (add "window-h" (number->string (get-h~ pos)))
                (add "window-v" (number->string (get-v~ pos))))
              (add "send-ready" "true")))))
    (delegate-process (update-exe) arguments: arguments wait-and-quit?: #t)))


;;;
;;;; At Update
;;;


(define (update-at-update)
  (add-stage-view (string-append "Updating " (jiri-title)) (if (was-update-new?) stage-setup-color stage-install-color))
  (update-tasks-done)
  (transfer-tasks-setup
    (lambda (transfer-task)
      (thread-start!
        (make-thread
          (lambda ()
            (update-launch transfer-task
              (lambda ()
                (update-app/data
                  (lambda (new-content?)
                    (update-done)))))))))))


(define (update-launch task cont)
  (let ((update-dir (make-directory (update-dir)))
        (root-dir (make-directory (current-root-dir))))
    (define (install-file filename)
      (install-file~
        (new-file~ update-dir filename)
        (new-file~ root-dir filename)))
    
    (define (install-directory-macos dirname libdir)
      (install-directory~
        (new-directory~ update-dir dirname)
        (new-directory~ root-dir dirname)
        progress-feedback: (lambda (action dir)
                             (when (and (eq? action 'entering)
                                        (pathname=? (get-parent~ dir) libdir))
                               (increase-pos~ task)))))
    
    (define (install-directory dirname)
      (install-directory~
        (new-directory~ update-dir dirname)
        (new-directory~ root-dir dirname)))
    
    (set-status~ task "Updating sirius")
    (let ((libdir (new-directory~ update-dir "Contents/MacOS/lib")))
      (let ((package-count (count-directories~ libdir)))
        (set-range~ task (new Range 0 package-count))
        (install-file "Contents/Info.plist")
        (install-directory-macos "Contents/MacOS" libdir)
        (install-directory "Contents/Resources")
        (set-done~ task)
        (post-event cont)))))


;;;
;;;; Work
;;;


(define (determine-stage-color dir)
  (if (not (file-exists? dir))
      stage-setup-color
    stage-install-color))


(define (update-app/data cont)
  (add-stage-view (string-append "Updating " (game-string)) (determine-stage-color (app-dir)))
  (pull-repository (jiri-app-section) (current-password) (app-dir)
    (lambda (new-content?)
      (add-stage-view (string-append "Updating " (get-title~ (jiri-data-section))) (determine-stage-color (data-dir)))
      (pull-repository (jiri-data-section) (current-password) (data-dir)
        cont))))


;;;
;;;; Done
;;;


(define (update-done)
  (set-title~ stage-view (or (jiri-play-ready) "Ready to play!"))
  (set-color~ stage-view stage-ready-color)
  (set-title~ status-view "Done")
  (set-cursor :arrow)
  (set-work-done? #t))


;;;
;;;; Layout
;;;


(define (layout)
  (add-view root-view)
  (add-view invite-view)
  (when close-view
    (add-view close-view))
  (when minimize-view
    (add-view minimize-view))
  
  (add-view connections-view)
  (let ((section (jiri-app-section)))
    (let ((connections (get-connections~ section))
          (default (get-default~ section)))
      (if (not connections)
          (let ((connection (get-connection~ section))
                (tree (locate~ connections-view 'tree)))
            (add-row~ tree children: (list (new Tree-Node title: (get-title~ connection))) user-data: connection)
            (set-single-selection~ tree 0))
        (assert default)
        (set-connection-change
          (lambda (connection)
            (set-connection~ (jiri-app-section) connection)))
        (let ((tree (locate~ connections-view 'tree)))
          (for-each (lambda (connection)
                      (add-row~ tree children: (list (new Tree-Node title: (get-title~ connection))) user-data: connection))
                    connections)
          (let ((default-connection (find connections default key: get-title~ test: equal? return: 'item)))
            (select-user-data-row~ tree default-connection)))
        (set-connection-change
          (lambda (connection)
            (set-connection~ (jiri-app-section) connection)
            (unless (get-started?~ connection)
              (pull-repository (jiri-app-section) (current-password) (app-dir)
                (lambda (new-content?)
                  ))))))))
  (add-stage-view (string-append "Updating " (game-string)) stage-install-color)
  
  (add-view percentage-view)
  (add-view downloaded-view)
  (add-view status-view)
  (add-view remaining-view)
  (add-view progress-view)
  (add-view play-view)
  ;; mimick launcher state
  (when (eq? stage 'update)
    ;;convert-to-task(set-title~ downloaded-view (string-append "Downloaded: " (number->string work-downloaded) "M"))
    ;;convert-to-task(set-title~ status-view (installing-title (jiri-update-title) 1 6))
    ;;convert-to-task(set-info~ progress-view (new Range 0. .1) (new Range 1 6))
    ;;convert-to-task(set-done~ progress-view)
    )
  (set-return-callback
    (lambda ()
      (when work-done?
        (play))))
  (set-quit-callback
    (quit-safely)))


;;;
;;;; Init
;;;


(set-init
  (lambda ()
    (prepare)
    (layout)))


;;;
;;;; Startup
;;;


(set-startup
  (lambda ()
    (update))))
